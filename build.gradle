plugins {
    id 'java'
}

group = 'org.air'
version = '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}

dependencies {
    // Cucumber + JUnit 4
    testImplementation 'io.cucumber:cucumber-java:7.14.0'
    testImplementation 'io.cucumber:cucumber-junit:7.14.0'
    testImplementation 'junit:junit:4.13.2'

    // Selenium
    testImplementation 'org.seleniumhq.selenium:selenium-java:4.21.0'

    // Rest Assured
    testImplementation 'io.rest-assured:rest-assured:5.4.0'
    testImplementation 'io.rest-assured:json-path:5.4.0'
    testImplementation 'io.rest-assured:json-schema-validator:5.4.0'

    // Jackson (optional)
    testImplementation 'com.fasterxml.jackson.core:jackson-databind:2.17.0'
}

java {
    sourceCompatibility = JavaVersion.VERSION_23
    targetCompatibility = JavaVersion.VERSION_23
}

test {
    useJUnit()
}

task testApi(type: Test) {
    useJUnit()
    systemProperty 'cucumber.filter.tags', '@api'
    include '**/ApiTestRunner*.class'
    testClassesDirs = sourceSets.test.output.classesDirs
    classpath = sourceSets.test.runtimeClasspath
    systemProperty 'cucumber.glue', 'api.stepdefinitions' // << Tambah ini
    testLogging {
        events "passed", "skipped", "failed"
    }
}

task testWeb(type: Test) {
    useJUnit()
    systemProperty 'cucumber.filter.tags', '@web'
    include '**/WebTestRunner*.class'
    testClassesDirs = sourceSets.test.output.classesDirs
    classpath = sourceSets.test.runtimeClasspath
    testLogging {
        events "passed", "skipped", "failed"
    }
}
